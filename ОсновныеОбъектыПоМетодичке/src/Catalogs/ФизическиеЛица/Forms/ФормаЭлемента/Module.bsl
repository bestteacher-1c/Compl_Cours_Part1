&НаКлиенте
Процедура ЗагрузитьФотоИзФайла(Команда)
	
	НачатьПомещениеФайла(
	Новый ОписаниеОповещения("ЗагрузитьФотоИзФайлаЗавершение", ЭтотОбъект)
	,,,Истина,УникальныйИдентификатор);

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьФотоИзФайлаЗавершение(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	
	Если Результат Тогда
	
		 АдресКартинки = Адрес;
	
	КонецЕсли;
	
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	Если ЭтоАдресВременногоХранилища(АдресКартинки) Тогда
		
		Фото = ПолучитьИзВременногоХранилища(АдресКартинки);
	
		ТекущийОбъект.Фотография = Новый ХранилищеЗначения(Фото);
	
	КонецЕсли;
	                                                 
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	Если ЭтоАдресВременногоХранилища(АдресКартинки) Тогда
		
		УдалитьИзВременногоХранилища(АдресКартинки);
		
		АдресКартинки = ПолучитьНавигационнуюСсылку(ТекущийОбъект.Ссылка,"Фотография");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	АдресКартинки = ПолучитьНавигационнуюСсылку(ТекущийОбъект.Ссылка,"Фотография");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьКартинкуВФайл(Команда)
	
	Если ЗначениеЗаполнено(АдресКартинки) Тогда
	
		ПолучитьФайл(АдресКартинки,,Истина);
	
	КонецЕсли;
	
КонецПроцедуры


